// Copyright 2015 The Go Authors. All rights reserved.
// Use of this source code is governed by a BSD-style
// license that can be found in the LICENSE file.

// values are specified using the following format:
// (op <type> [auxint] {aux} arg0 arg1 ...)
// the type and aux fields are optional
// on the matching side
//  - the type, aux, and auxint fields must match if they are specified.
// on the generated side
//  - the type of the top-level expression is the same as the one on the left-hand side.
//  - the type of any subexpressions must be specified explicitly.
//  - auxint will be 0 if not specified.
//  - aux will be nil if not specified.

// blocks are specified using the following format:
// (kind controlvalue succ0 succ1 ...)
// controlvalue must be "nil" or a value expression
// succ* fields must be variables
// For now, the generated successors must be a permutation of the matched successors.

// constant folding
(Add <t> (Const [c]) (Const [d])) && is64BitInt(t) -> (Const [c+d])
(Sub <t> (Const [c]) (Const [d])) && is64BitInt(t) -> (Const [c-d])
(Mul <t> (Const [c]) (Const [d])) && is64BitInt(t) -> (Const [c*d])
(Rsh <t> (Const [c]) (Const [d])) && is64BitInt(t) && d >= 0 -> (Const [c>>uint(d)])
(Lsh <t> (Const [c]) (Const [d])) && is64BitInt(t) && d >= 0 -> (Const [c<<uint(d)])
(IsInBounds (Const [c]) (Const [d])) -> (Const {inBounds(c,d)})
(Eq (Const [c]) (Const [d])) -> (Const {c == d})
(Neq (Const [c]) (Const [d])) -> (Const {c != d})
(Less (Const [c]) (Const [d])) -> (Const {c < d})
(Leq (Const [c]) (Const [d])) -> (Const {c <= d})
(Greater (Const [c]) (Const [d])) -> (Const {c > d})
(Geq (Const [c]) (Const [d])) -> (Const {c >= d})

// constant operations
(Less <t> (Const [c]) _) && c < 0 && !t.IsSigned() -> (Const {true})
(Leq <t> (Const [0]) _) && !t.IsSigned() -> (Const {true})
(Greater <t> (Const [c]) _) && c < 0 && !t.IsSigned() -> (Const {false})
(Geq <t> _ (Const [0])) && !t.IsSigned() -> (Const {true})
(Mul (Const [0]) _) -> (Const [0])
(Mul _ (Const [0])) -> (Const [0])
(Sub x y) && x == y -> (Const [0])
(Rsh <t> _ (Const [c])) && !t.IsSigned() && c >= 8*t.Size() -> (Const [0])
(Lsh <t> _ (Const [c])) && !t.IsSigned() && c >= 8*t.Size() -> (Const [0])

// tear apart slices
// TODO: anything that generates a slice needs to go in here.
(SlicePtr (Load ptr mem)) -> (Load ptr mem)
(SliceLen (Load ptr mem)) -> (Load (Add <ptr.Type> ptr (Const <config.Uintptr> [config.ptrSize])) mem)
(SliceCap (Load ptr mem)) -> (Load (Add <ptr.Type> ptr (Const <config.Uintptr> [config.ptrSize*2])) mem)

// indexing operations
// Note: bounds check has already been done
(ArrayIndex (Load ptr mem) idx) -> (Load (PtrIndex <v.Type.PtrTo()> ptr idx) mem)
(PtrIndex <t> ptr idx) -> (Add ptr (Mul <config.Uintptr> idx (Const <config.Uintptr> [t.Elem().Size()])))
(StructSelect [idx] (Load ptr mem)) -> (Load (OffPtr <v.Type.PtrTo()> [idx] ptr) mem)

// big-object moves
// TODO: fix size
(Store dst (Load <t> src mem) mem) && t.Size() > 8 -> (Move [t.Size()] dst src mem)

// string ops
(Const <t> {s}) && t.IsString() -> (StringMake (OffPtr <TypeBytePtr> [2*config.ptrSize] (Addr <TypeBytePtr> {config.fe.StringSym(s.(string))} (SB <config.Uintptr>))) (Const <config.Uintptr> [int64(len(s.(string)))])) // TODO: ptr
(Load <t> ptr mem) && t.IsString() -> (StringMake (Load <TypeBytePtr> ptr mem) (Load <config.Uintptr> (OffPtr <TypeBytePtr> [config.ptrSize] ptr) mem))
(StringPtr (StringMake ptr _)) -> ptr
(StringLen (StringMake _ len)) -> len
(Store dst str mem) && str.Type.IsString() -> (Store (OffPtr <TypeBytePtr> [config.ptrSize] dst) (StringLen <config.Uintptr> str) (Store <TypeMem> dst (StringPtr <TypeBytePtr> str) mem))

(If (Const {c}) yes no) && c.(bool) -> (Plain nil yes)
(If (Const {c}) yes no) && !c.(bool) -> (Plain nil no)
