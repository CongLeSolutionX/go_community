// Copyright 2015 The Go Authors. All rights reserved.
// Use of this source code is governed by a BSD-style
// license that can be found in the LICENSE file.

// values are specified using the following format:
// (op <type> [auxint] {aux} arg0 arg1 ...)
// the type and aux fields are optional
// on the matching side
//  - the type, aux, and auxint fields must match if they are specified.
// on the generated side
//  - the type of the top-level expression is the same as the one on the left-hand side.
//  - the type of any subexpressions must be specified explicitly.
//  - auxint will be 0 if not specified.
//  - aux will be nil if not specified.

// blocks are specified using the following format:
// (kind controlvalue succ0 succ1 ...)
// controlvalue must be "nil" or a value expression
// succ* fields must be variables
// For now, the generated successors must be a permutation of the matched successors.

// constant folding
(IsInBounds (Const [c]) (Const [d])) -> (Const {inBounds(c,d)})
(Mul <t> (Const [c]) (Const [d])) && is64BitInt(t) -> (Const [c*d])
(Mul (Const [0]) _) -> (Const [0])
(Mul _ (Const [0])) -> (Const [0])

// 64 bits, sorted by operation.
(Add64 (Const [c]) (Const [d])) -> (Const [c+d])
(Add64U (Const [c]) (Const [d])) -> (Const [c+d])
(AddPtr (Const [c]) (Const [d])) -> (Const [c+d])
(Mul64 (Const [c]) (Const [d])) -> (Const [c*d])
(Mul64U (Const [c]) (Const [d])) -> (Const [c*d])
(MulPtr (Const [c]) (Const [d])) -> (Const [c*d])
(IsInBounds (Const [c]) (Const [d])) -> (Const {inBounds(c,d)})

(Eq64 (Const [c]) (Const [d])) -> (Const {c == d})
(Geq64 (Const [c]) (Const [d])) -> (Const {c >= d})
(Geq64U _ (Const [0])) -> (Const {true})
(Greater64 (Const [c]) (Const [d])) -> (Const {c > d})
(Greater64U (Const [0]) _) -> (Const {false})
(Leq64 (Const [c]) (Const [d])) -> (Const {c <= d})
(Leq64U (Const [0]) _) -> (Const {true})
(Less64 (Const [c]) (Const [d])) -> (Const {c < d})
(Less64U _ (Const [0])) -> (Const {false})
(Lsh64 _ (Const [c])) && c >= 64 -> (Const [0])
(Lsh64 (Const [c]) (Const [d])) && d >= 0 -> (Const [c<<uint(d)])
(Neq64 (Const [c]) (Const [d])) -> (Const {c != d})
(Rsh64 (Const [c]) (Const [d])) -> (Const [c>>uint(d)])
(Rsh64U _ (Const [c])) && c >= 64 -> (Const [0])
(Rsh64U (Const [c]) (Const [d])) -> (Const [c>>uint(d)])
(Sub64U x y) && x == y -> (Const [0])
(Sub64 x y) && x == y -> (Const [0])

// TODO: Add constant operations for 8, 16 and 32 bit sizes.
// Special care must be given to overflowing. For example:
// a, b := uint8(100), uint8(200)
// int(a+b) == 44 && int(a+b) != 300

// tear apart slices
// TODO: anything that generates a slice needs to go in here.
(SlicePtr (Load ptr mem)) -> (Load ptr mem)
(SliceLen (Load ptr mem)) -> (Load (AddPtr <ptr.Type> ptr (Const <config.Uintptr> [config.PtrSize])) mem)
(SliceCap (Load ptr mem)) -> (Load (AddPtr <ptr.Type> ptr (Const <config.Uintptr> [config.PtrSize*2])) mem)

// indexing operations
// Note: bounds check has already been done
(ArrayIndex (Load ptr mem) idx) -> (Load (PtrIndex <v.Type.PtrTo()> ptr idx) mem)
(PtrIndex <t> ptr idx) -> (AddPtr ptr (MulPtr <config.Uintptr> idx (Const <config.Uintptr> [t.Elem().Size()])))
(StructSelect [idx] (Load ptr mem)) -> (Load (OffPtr <v.Type.PtrTo()> [idx] ptr) mem)

// big-object moves
// TODO: fix size
(Store dst (Load <t> src mem) mem) && t.Size() > 8 -> (Move [t.Size()] dst src mem)

// string ops
(Const <t> {s}) && t.IsString() -> (StringMake (Addr <TypeBytePtr> {config.fe.StringData(s.(string))} (SB <config.Uintptr>)) (Const <config.Uintptr> [int64(len(s.(string)))]))
(Load <t> ptr mem) && t.IsString() -> (StringMake (Load <TypeBytePtr> ptr mem) (Load <config.Uintptr> (OffPtr <TypeBytePtr> [config.PtrSize] ptr) mem))
(StringPtr (StringMake ptr _)) -> ptr
(StringLen (StringMake _ len)) -> len
(Store dst str mem) && str.Type.IsString() -> (Store (OffPtr <TypeBytePtr> [config.PtrSize] dst) (StringLen <config.Uintptr> str) (Store <TypeMem> dst (StringPtr <TypeBytePtr> str) mem))

(If (Not cond) yes no) -> (If cond no yes)
(If (Const {c}) yes no) && c.(bool) -> (Plain nil yes)
(If (Const {c}) yes no) && !c.(bool) -> (Plain nil no)
